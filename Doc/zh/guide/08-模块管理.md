# 模块管理  

`batScript`和`C`的`#include`类似的模块管理机制.  
使用`lib`和`pkg`作为关键字.导包语句必须位于有效代码部分的开头.不能重复导入包.  
由于导包相当于将代码直接复制在文件的开头,且`batScript`没有设计命名空间机制,因此要特别留意命名冲突的问题.  
对于不对外暴露的私有部分,我们约定:以`_<包名>_`开头.例如:  

```batscript
/*
:file: MyLib.bs
*/

set __MyLib__mylib_private: bol;
```

语法提示插件会自动不补全对应的内容.不过,需要注意的是,对于导入的文件而言,这些内容还是可以访问的,不是真正的权限控制.  

## 导入库  

使用`lib`关键字导入库:  

```batscript
lib Std;
```

会自动索引位于`bsc.exe`同目录下的`bslib`对应的标准库文件,省略`.bs`后缀名.  

> 标准库的名称必须采用大驼峰命名,且不包括非英文字母的名称  

### 最常用的标准库  

以下列举了一些最常用的标准库内容:  

#### Std库

Std库提供基础的输入输出功能,是最常用的库.

- `echo(msg: str)`: 输出文本,不换行
- `echoline(msg: str)`: 输出文本并换行
- `pause()`: 暂停程序,等待用户按键

```batscript
lib Std;

echoline("Welcome to batScript!");
echo("Press any key to continue...");
pause();
```

- `input(prompt: str) -> str`: 显示提示并获取用户输入

```batscript
lib Std;

proc greetUser()
{
    set name = input("Enter your name: ");
    echoline("Hello, " .. name .. "!");
}

greetUser();
```

#### Str库

Str库提供字符串处理相关函数.

- `strReplace(source: str, old: str, new: str) -> str`: 替换字符串中的内容
- `strCompare(str1: str, str2: str) -> num`: 比较两个字符串,返回-1(str1<str2)、0(相等)或1(str1>str2)
- `strIndex(source: str, target: str) -> num`: 查找子字符串首次出现的位置,未找到返回-1
- `strCount(source: str, target: str) -> num`: 统计子字符串在源字符串中出现的次数
- `strUpper(s: str) -> str`: 将字符串转为大写
- `strLower(s: str) -> str`: 将字符串转为小写
- `strTrim(s: str) -> str`: 去除字符串首尾空白字符
- `strSplit(source: str, delimiter: str) -> arr<str>`: 按分隔符拆分字符串为数组
- `strJoin(arr: arr<str>, separator: str) -> str`: 用分隔符连接字符串数组
- `strStartsWith(source: str, prefix: str) -> bol`: 判断字符串是否以指定前缀开头
- `strEndsWith(source: str, suffix: str) -> bol`: 判断字符串是否以指定后缀结尾
- `strSubstring(source: str, start: num, length: num) -> str`: 提取子字符串

```batscript
lib Str;
lib Std;

# 替换
set text = "Hello World";
set new_text = strReplace(text, "World", "batScript");  # "Hello batScript"

# 比较
set cmp1 = strCompare("abc", "abd");  # -1
set cmp2 = strCompare("test", "test");  # 0
set cmp3 = strCompare("xyz", "abc");  # 1

# 查找子字符串
set pos = strIndex("batScript is awesome", "Script");  # 3
set not_found = strIndex("Hello", "World");  # -1

# 计次
set count = strCount("banana", "a");  # 3
set count2 = strCount("Hello World", "o");  # 2

# 大小写转换
set upper = strUpper("hello");  # "HELLO"
set lower = strLower("WORLD");  # "world"

# 清楚空白
set trimmed = strTrim("  hello  ");  # "hello"

# 拆分和聚合
set parts = strSplit("a,b,c", ",");  # ["a", "b", "c"]
set joined = strJoin(parts, "-");  # "a-b-c"

# 前缀和后缀检测
set starts = strStartsWith("batScript", "bat");  # True
set ends = strEndsWith("test.bs", ".bs");  # True

# 切片
set sub = strSubstring("Hello World", 0, 5);  # "Hello"

proc processUserInput()
{
    set user_input = input("Enter text: ");
    
    if (strIndex(user_input, "batScript") >= 0)
    {
        echoline("Keyword detected!");
    }
    
    set trimmed = strTrim(user_input);
    set cleaned = strReplace(trimmed, "bad", "***");
    
    set upper = strUpper(cleaned);
    echoline("Processed: " .. upper);
}
```

#### Math库

Math库提供数学运算相关的函数.

- `isOdd(n: num) -> bol`: 判断是否为奇数
- `isEven(n: num) -> bol`: 判断是否为偶数
- `floor(n: num) -> num`: 向下取整
- `ceil(n: num) -> num`: 向上取整
- `round(n: num) -> num`: 四舍五入取整
- `pow(base: num, exponent: num) -> num`: 计算幂运算
- `sqrt(n: num) -> num`: 计算平方根
- `abs(n: num) -> num`: 计算绝对值
- `max(a: num, b: num) -> num`: 返回较大值
- `min(a: num, b: num) -> num`: 返回较小值

Math库也提供了常用的数学常量:

- `PI`: 圆周率 π (3.14159265358979323846...)
- `PI_SIMP`: 简化的圆周率 (3.14)
- `E`: 自然对数的底 e (2.71828182845904523536...)
- `E_SIMP`: 简化的自然对数底 (2.72)

```batscript
lib Math;
lib Std;

set num = 7;
if (isOdd(num))
{
    echoline("7 is odd");
}

set result = pow(2, 10);     # 1024
set root = sqrt(16);         # 4
set integer = floor(3.14);   # 3
set rounded = round(3.7);    # 4

set circle_area = PI * pow(5, 2);
echoline("Circle area: " .. circle_area);

set approx_area = PI_SIMP * 25;
echoline("Approximate area: " .. approx_area);

set exp_value = pow(E, 2);
echoline("e^2 = " .. exp_value);
```

```batscript
lib Math;

func calculateCircleCircumference(radius: num) -> num
{
    return 2 * PI * radius;
}

func estimateCircleCircumference(radius: num) -> num
{
    return 2 * PI_SIMP * radius;
}

set precise = calculateCircleCircumference(10);   # 62.83185307...
set estimated = estimateCircleCircumference(10); # 62.8
```

#### Cmd库

Cmd库用于配置命令行窗口的外观和行为.

- `setCmdTitle(title: str)`: 设置窗口标题
- `setCmdColor(foreground: num, background: num)`: 设置文字和背景颜色
- `clearCmd()`: 清空屏幕
- `setCmdSize(width: num, height: num)`: 设置窗口大小

Cmd库也提供了一些预设的色彩常量:  

- `CMD_BLACK`: 黑色 (0)
- `CMD_BLUE`: 蓝色 (1)
- `CMD_GREEN`: 绿色 (2)
- `CMD_CYAN`: 青色 (3)
- `CMD_RED`: 红色 (4)
- `CMD_MAGENTA`: 洋红 (5)
- `CMD_YELLOW`: 黄色 (6)
- `CMD_WHITE`: 白色 (7)
- `CMD_GRAY`: 灰色 (8)
- `CMD_LIGHT_BLUE`: 淡蓝 (9)
- `CMD_LIGHT_GREEN`: 淡绿 (10)
- `CMD_LIGHT_CYAN`: 淡青 (11)
- `CMD_LIGHT_RED`: 淡红 (12)
- `CMD_LIGHT_MAGENTA`: 淡洋红 (13)
- `CMD_LIGHT_YELLOW`: 淡黄 (14)
- `CMD_BRIGHT_WHITE`: 亮白 (15)

```batscript
lib Cmd;
lib Std;

setCmdTitle("My batScript Program");
setCmdColor(CMD_LIGHT_GREEN, CMD_BLACK);
clearCmd();
echoline("Using color constants!");

setCmdColor(10, 0);  

setCmdColor(CMD_YELLOW, CMD_BLUE);
echoline("Warning message");

setCmdColor(CMD_RED, CMD_WHITE);
echoline("Error message");

setCmdColor(CMD_WHITE, CMD_BLACK); 
```

```batscript
lib Cmd;
lib Std;

proc setupWindow()
{
    setCmdTitle("Data Analysis Tool v1.0");
    setCmdSize(100, 30);
    setCmdColor(CMD_LIGHT_CYAN, CMD_BLACK);
    clearCmd();
}

setupWindow();
echoline("Window configured!");
```

> **完整库参考**: 更多库和详细的函数/过程说明,请参考[库索引](../../manual/库/库索引.md)

## 导入文件  

使用`pkg`导入文件.导入的文件默认从相对路径开始索引,也支持全局路径.目录分隔符既可使用`/`,也可以使用`\`.  
和`lib`不一样的是,文件路径要用原始字符串` `` `,且不可省略后缀名.  

```batscript
pkg `MyBs.bs`;
pkg `../System/Config.bs`
pkg `C:\\Windows.bs`
```

## 循环引入  

如果一个包A依赖包B,包B依赖包C,而包C又依赖包A,这种情况就称为循环引入.  
`batScript`不支持循环引入.可以使用`bsc`的`tree`命令检查一个包的引入链:  

```powershell
bsc tree MyLib.bs
```

---  
**导航**  

[上一个](./07-函数和过程.md) | [下一个](./09-内嵌.md)  
[教程目录](./01-教程目录.md) | [手册导览](../manual/手册导引.md) | [返回 README](../../../README-zh.md)
