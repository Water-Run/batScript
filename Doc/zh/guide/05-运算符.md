# 运算符

`batScript`提供了更接近现代写法的简洁符号系统.

> 符号分为两类: 单目符号和双目符号.根据[batScript规范](),双目符号之间需要添加空格  

## 计算符号

计算符号即执行四则运算的符号.

### 四则运算: `+` `-` `*` `/`

`+`,`-`,`*`,`/`即加减乘除符号,均为双目运算符.符号的两端均为`num`类型,表达式的结果也是`num`类型.

*示例:*

```batscript
set num_var: num;
num_var = 1 + 1 * 2 / 3; # 1.66666666666666666666666666666666 # 保留32位小数,采用直接截断
```

特别的,当出现除零的运算时:

```batscript
set divide_zero = 1 / 0;
```

会导致除零错误:

```powershell
batscript: Divide by zero in `1 / 0`
```

## 赋值与关系符号

### 赋值符 `=`

赋值符将右侧的字面量赋值给左侧的变量.

含赋值符表达式的结果是`Nil`.

*示例:*

```batscript
set var_1: num = 1;
set res_1 = (var_1 = 2); # res_1 == Nil
```

在`batScript`中,赋值符可以进行链式的从右向左进行赋值:

*示例:*

```batscript
set var_1: num = 1;
set var_2: num = var_1 = 3; # var_1 == var_2 == 3
```

### 值关系判断符 `==` `~=` `>=` `>` `<` `<=`

值关系判断用于判断值之间的关系.被判断的值必须是相同类型的.

`==`判断两个值是否相等.`~=`判断两个值是否不相等.`>=`,`>`,`<=`,`<`依次判断左侧的值是否大等于,大于,小等于,小于右边的值.

值判断表达式返回布尔值`True`或`False`,对应判断的结果.

只有`num`类型可以进行大小比较.  

值关系判断符可以以链式的方式使用,判断顺序从左到右.

*示例:*

```batscript
set var_1 = 1;
set var_2 = var_1 == 1; # True
set var_3 = b[var_2 ~= True]; # b[False]: 自动推导为arr<bol>
set var_4 = @var_3 >= 1; # True
set var_5 = 3 <= 4 < 5 > -1; # True
```

### 优先级符号 `(` `)`

`()`用于表示运算的优先级.最里层`()`内的表达式会先进行计算.

*示例:*

```batscript
set var_1 = (1 + 2) / -3; # -1
```

## 可迭代类型运算符

这些运算符用于可迭代类型之中,即`str`和`arr`.

### 可迭代体取长 `@`

`@`运算符获取可迭代体的长度,返回`num`.

*示例:*

```batscript
set var_1 = @"00000"; # 5
```

### 可迭代体拼接 `..`

`..`用于拼接符号左右侧的可迭代体,返回拼接后的结果.

*示例:*

```batscript
set var_1 = n[-1, 0, 1] .. n[2]; # n[-1, 0, 1, 2]
```

`..`也是字符串拼接最常用的实现.

### 可迭代体追加 `^`

`^`符号用于在可迭代体左侧或右侧追加对应的可迭代单元.返回追加后的可迭代体.

*示例:*

```batscript
set var_1 = "Hello" ^ "World"; # HelloWorld: 对于字符串,可迭代单元就是一个子字符串,表现和 `..` 一致
set var_2 = 1 ^ n[] ^ n[2, 3]; # n[1, 2, 3]: 可以链式追加,从右往左处理.`[]`等效`Nil`,自动构建数组
```

### 可迭代体变长 `%%`

在可迭代体的右侧添加`%%`符号及对应修改后的长度整数用于修改可迭代体的长度.

当变长后的长度小于修改前的长度时,将从结尾开始截断;当变长后的长度大于修改前的长度时,`str`填充`" "`,`arr`自动填充`Nil`.

*示例:*

```batscript
set var_1 = n[-1, 0, 1] %% 4; # n[-1, 0, 1, Nil]
set var_1 = var_1 %% 1; # n[-1]
```

## 其它运算符

### 取类型字符串 `&`

取类型字符串用于获取对应类型字符串表示.依次为`"num"`,`"bol"`,`"str"`,`"arr<num>"`,`"arr<bol>"`,`"arr<str>"`.

*示例:*

```batscript
set type_1 = &123.45; # "num"
```

> 不能对字面量`Nil`取类型

### 类型转换 `|`

类型转换运算符用于转换值为其它类型.表达式的返回值为转换后的值.

具体转换准则见[类型转换表]().

*示例:*

```batscript
set num_from_str = num | "100.2"; # 100.2
```

### 取反 `!`

取反运算符用于反转指定的值.不同类型的变量遵循不同逻辑:

| 类型 | 说明 |
|---|---|
| `num` | 正负转换 |
| `bol` | 布尔反转 |
| `str` | 反转字符串 |
| `arr` | 反转数组 |

返回反转后的值.

*示例:*

```batscript
set var_1 = !5; # -5
set var_2 = !False; # True
set var_3 = !"123"; # "321"
set var_4 = ![True, False, Nil]; # [Nil, False, True]
```

> 当值为`Nil`时,转换无效  

### 取原始符 `$`

取原始运算符用于获取对应的变量,常量,函数/过程在脚本文件中对应的原始关键名字符串.

参阅[batScript实现: 名称]().

*示例:*

```batscript
proc doNothing()
{
}

set native_name = $doNothing; # ____bsp________llulllllle_donothing1
```

> 常用于直接嵌入原生逻辑中获取值.不过,这也会导致完全跳过编译器的检查和多数运行时,容易导致异常,需要非常小心的使用  

## 逻辑关键字

最后,介绍`batScript`中的逻辑关键字:且`and`,或`or`和非`not`.均返回布尔值:

*示例:*

```batscript
set bol_var = not (False or True and False); # True
```

逻辑关键字的左右两侧都必须是一个布尔表达式,否则会导致异常:

```batscript
set not_nil = not Nil; # symbol error
```

---
**导航**

[上一个](./04-类型系统.md) | [下一个](./06-控制流.md)  
[教程目录](./01-教程目录.md) | [手册导览](../manual/手册导引.md) | [返回 README](../../../README-zh.md)  
