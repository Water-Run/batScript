# 类型系统

## 概览

在原生的`.bat`中, 变量只能是字符串类型(或是通过指令处理整数). 这显然在使用中非常不便利.

`batScript`提供了一个基本的类型系统:  

- `num`: 数字型. 整数位可无限长, 小数位默认在超出16位时截断(编译时可设置), 可为负数  
- `str`: 字符串型, 不限长度  
- `bol`: 布尔型, `True`和`False`  

以及对应类型的一维数组形式:  

- `arr`: 一维数组, 指定类型(声明时使用`<>`, 如`arr<num>`), 可变长  

`arr`和`str`被称为可迭代类型. 所有类型的值可为空值`Nil`.  

## 变量声明

### 变量命名

在`BatScript`中, 变量可以由大小写字母, 下划线和数字组成. `BatScript`区分大小写.  

变量名中必须包含至少一个字母. 数字不能位于开头. 变量名最长不超过48个字符.  

`BatScript`规范建议变量采用小写+下划线, 常量采用大写+下划线的命名风格.  

> 在`BatScript`中, 名称不能以四个连续的`_`开头, 如`____VAR_123`. 这涉及到`BatScript`的内部实现机制  

### 声明变量

在`batScript`中, 通过`set`声明变量. 在变量名之后,需要通过`: [类型]`声明变量的类型.  

变量采用小写+下划线的命名规范:  

```batscript
set var_1: num; # 声明num类型变量 var_1, 默认值为 Nil (空值)
```

在声明时, 可以通过`=`为变量赋值:  

```batscript
set var_1: bol = True; # 声明bol类型变量 var_1, 并赋初始值 True
set var_2: str = "MyVar"; # 声明str类型变量 var_2, 并赋初始值 "MyVar"
set var_3: num = 1 + 1; # 声明的字面量可以是一个表达式
```

使用`let`声明的变量将被视为常量, 常量一经创建, 不可修改.  

常量采用大写+下划线的命名规范:  

```batscript
let CONST_1 = True; # 声明常量CONST_1, 其值为True
let CONST_2: arr<num>; # 声明常量但不赋值, 其值将只为空值. 这会给出警告
```

> `var`关键字用于声明局部变量, 其语法和`set`一致, 用在循环体/选择体/函数或过程中  

## 内建字面量

### 空值

在`batScript`中, 变量在声明时默认为`Nil`, 直到被赋值. 在赋值之后, 也可以使用`[变量] = Nil`重新赋为空值.  

一种判断是否为空值的方式是使用`[变量] == Nil`判断. 不过, 更推荐使用判空运算符`?`:  

```batscript
set var_1: num; # Nil
?var_1; # True
var_1 = 1;
?var_1; # False
```

> 务必正确的在代码中处理空值,否则可能导致空值异常. 参阅[空值的标准处理]()

### 布尔字面量  

布尔字面量包括两种: `True`和`False`.  

> 注意在`BatScript`中, 布尔变量不保证不是`True`就是`False`: 还有可能是空值`Nil`  

### 数字字面量  

数字字面量包括一种: `INF`, 表示无限大, 可用于比较运算中. 自然, `-INF`表示无限小.  

## 各类型的详细说明

在`BatScript`中,类型分为两大类: 可迭代类型和不可迭代类型.  

其中, 不可迭代类型即为不可逐个访问元素的类型, 即单个值, 包括`num`和`bol`. 可迭代类型与相反, 为可逐个访问元素的类型,包括`str`和`arr`.  

> 在`BatScript`中,所有的变量都是值类型变量. `BatScript`不存在引用的概念  

### 不可迭代类型

#### `bol`: 布尔类型

最简单的类型就是布尔类型. 非空的布尔变量要么是`True`, 要么是`False`.  

```batscript
set bol_var: bol = True;
bol_var = False;
```

#### `num`: 数字类型

数字类型是`batScript`内建的自定义类型, 表示数字. 其中, 整数位可以有无限位, 小数位默认情况下保留16位(溢出截断), 有符号.  

数字间可以进行计算, 计算结果会保留最少小数位和最少符号的形式(负数会包含负号`-`,正数默认没有符号).  

```batscript
set num_1: num = 1;
set num_2: num;

num_2 = num_1 + 1; # num_2 = 2
num_2 = num_2 - 0.5; # num_2 = 1.5;
num_2 = num_2 - 5 * num_1; # num_2 = -3.5
num_2 = +10000000000000000000000000000000000000000000000000000000000000000000; # num_2 = 10000000000000000000000000000000000000000000000000000000000000000000
num_2 = num_2 - 0.0000000000000000000000000000001; # num_2 = 9999999999999999999999999999999999999999999999999999999999999999999.9999999999999999999999999999999
num_2 = 00123; # num_2 = 123
```

> 当小数位超出16位时,将会直接截断

数字可以进行格式化处理.最常用的是`Math`库提供的系列函数:

```batscript
lib Math;

# 小数位数保留: 截断, 向下, 向上, 四舍五入
num_1 = truncate(3.1415926, 2); # num_1: 3.14
num_1 = floor(3.1415926, 3); # num_1: 3.141
num_1 = ceil(3.1415926, 1); # num_1: 3.2
num_1 = roundDecimals(3.1415926, 2); # num_1: 3.14
```

> 详见[Math库](../manual/库/Math库.md)

### 可迭代类型

#### `str`: 字符串类型

字符串类型`str`用于表达一个字符串.

最标准的字符串表示是使用`""`,如`"String"`.  

```batscript
set str_1: str;
str_1 = "My String";
```

使用`''`表示的字符串叫做`b-string`,可以理解为一个简化版`Python` `f-string`.`b-string`内可以用`{}`嵌入一个变量或常量.  

```batscript
set version = '1.0'
set str_2 = 'version is {version}'
```

需要注意的是,`b-string`的功能很基础,除单元运算符外,你不能在其中嵌入一个表达式.  

使用` `` `表示原始字符串.原始字符串内的内容不进行转义,在接下来的转义符部分进行更详细的讲解.  

##### 转义符

`""`和`''`支持很有限的C风格的基础转义符:  

```batscript
lib Std;

/*
Hello
    World
*/
echo("Hello\n\tWorld");
```

包括:  

|符号|说明|
|---|---|
|`\n`|换行|
|`\t`|四个空格|
|`\"`|双引号|
|`\'`|单引号|
|`\\`|反斜杠|

` `` `称为原始字符串.其内的内容将保留原有格式.例如,以下是和上述基本相同的代码,只是使用了` `` `:

```batscript
lib Std;

/*
Hello\n\tWorld
*/
echo(`Hello\n\tWorld`);
```

##### 输出

最常用的输出函数/过程`echoline()`接受的参数是一个字符串:

```batscript
lib Std;

set output_str: str = "HelloWorld!";
echoline(output_str); # HelloWorld!
```

#### `arr`: 一维数组类型

一维数组是`batScript`支持的独特类型,为`bat`提供了有限的迭代功能.  
一维数组的每一项都是且只能是所定义的类型.通过`<T>[]`.`<T>`表示对应一位数组的类型首字母,如`n[]`.一维数组也支持自动类型推导.  
数组的每一项之间通过`,`隔开,如`n[1, 2, 3]`.支持尾逗号,如`b[true,]`.  

```batscript
set num_arr: arr<num> = n[0, 3.14, -100];
set str_arr: arr<str>; # s[]
```

> 之所以只实现为一位数组而不是列表等更高级的数据类型,主要考虑基于实现上难度和多数`.bat`本身较为简单固定的因素考虑

```batscript
set arr_1: arr<num> = n[]; # 以空数组初始化
```

##### 一维数组的高级构建

常用库`Iterateable`的`build`系列函数/过程提供了一位数组的一些高级构建.例如,以下函数/过程用来构造均为某个值的一位数组:

```batscript
lib Array;

set nuums_arr: arr<num> = buildNumArrWithValue(1, 5); # n[1, 1, 1, 1, 1]
set strs_arr: arr<str> = buildStrArrWithValue("", 2); # s["", ""]
set bols_arr: arr<bol> = buildbolArrWithValue(true, 0); # b[]
```

又比如,使用`buildNumArrByIncrease()`可以按照指定的递增值构建数组:

```batscript
lib Array;

set arr_1: arr<num> = buildNumArrByIncrease(0, 1, 3); # 0作为第一项, 步进为1, 3个元素: n[0, 1, 2]
```

高级构建的详细文档见[Array库](../manual/库/Array库.md).

### 可迭代类型的通用语法

#### 索引

使用`[下标]`进行索引,获取指定位置的元素.下标是一个整数,起点是`0`:

```batscript
set arr_1: arr<num> = n[-1, 0, 1];
set arr_2: arr<str> = s["-1", "0", "1"];
set str_1: str = "String is iterateable.";

set var_1: num = arr_1[2]; # 1
set var_2: str = arr_2[0]; # "-1"
set var_3: str = str_1[1]; # "t"
```

##### 索引越界

在`batScript`中,访问越界的索引(即比当前的元素更多),将导致异常.如下列代码:

```batscript
set arr_1: arr<num> = n[1, 2, 3][3]; # 索引越界.两个[][]的意思是,访问一维数组[1, 2, 3]的第3项
```

会导致异常信息如下:

```powershell
batscript: Index out of range in array `arr_1` when trying to get value of index 3 (length: 3)
```

如果不希望导致该异常,一种方式是在索引前取长,添加逻辑进行预处理.例如,以下逻辑设定了自定义的异常:

```batscript
set index: num = 4;
set string: str = "abc";

if (index - 1 >= @string)
{
    err 'String {string} is shorter than' .. index + 1;
}

set content: str = string[index];
```

输出为:

```powershell
batscript: String "abc" is shorter than 4
```

或者,使用`Iterateable`库中的`safeGet`系列函数/过程.如:

```batscript
lib Iterateable;

safeGetArrbol(b[true], 1); # 访问第二项.返回Nil
```

`safeGet`的更多系列函数/过程,见[Iterateable库](../manual/库/Iterateable库.md)中的详细说明.

#### 取长

使用`@`运算符可以获取对应可迭代类型的长度:

```batscript
set empty_array: arr<str> = s[];

set len_1 = @"string"; # 6
set len_2 = @empty_array; # 0
```

#### 拼接

使用`..`运算符可以拼接相同的可迭代对象:

```batscript
let my_str = "Hello " .. "World!"; # Hello World
set my_arr_1 = b[True, False];
set my_arr_2 = b[False, True];
my_arr_1 = my_arr_1 .. my_arr_2; # b[True, False, False, True]
```

#### 追加

使用`^`运算符可以在可迭代体的开头或结尾追加一个可迭代单元(即对于字符串,和拼接近似;对于数组,则是对应类型的单元).

当追加位于表达式左侧时,追加在开头;否则,追加到结尾.语法表示为`[可迭代体] ^ [可迭代单元]`(追加到结尾)或`[可迭代单元] ^ [可迭代体]`:

```batscript
let my_str = "1234" ^ "5"; # "12345"
let my_arr = 0 ^ n[1, 2, 3, 4]; # n[0, 1, 2, 3, 4]
```

需要注意的是,`^`优先执行追加到结尾.例如下列表达式:

```batscript
let my_arr = 0 ^ [1] ^ 2;
```

等价于

```batscript
let my_arr = 0 ^ ([1] ^ 2);
```

#### 变长

使用`%%`运算符可以修改可迭代体的长度.

当变长后的长度长于原先长度时:

- 字符串: 追加`" "`
- 数组: 追加`Nil`

当变长后的长度小于原先长度时,则进行截断.

语法表示为:`[可迭代体] %% [变长后的长度]`:

```batscript
set str_1 = "1234" %% 5; # "1234 "
set arr_1 = n[1, 2, 3] %% 1; # n[1]
set arr_2 = n[1];
arr_2 = arr_2 %% (@arr_2 + 1); # 变长一位: n[1, Nil]
```

## 类型信息获取与转换

### 获取类型字符串

使用`&`运算符,可以获取变量类型的字符串表示:

```batscript
&3.14; # "num"
&"3.14"; # "str"
&b[True]; # "arr<bol>"
&Nil; # "nil"
```

### 类型转换

在`batScript`中,类型可以使用`|`进行转换.语法是,`[转换后的类型] | [被转换的值]`:

```batscript
set num_to_string: str = str | 123; # "123"
set string_to_bol: bol = bol | "true"; # True
```

> 具体转换的逻辑,见[类型转换逻辑](../manual/语法/类型转换逻辑.md)

---
**导航**

[上一个](./03-语法基础.md) | [下一个](./05-运算符.md)  
[教程目录](./01-教程目录.md) | [手册导览](../manual/手册导引.md) | [返回 README](../../../README-zh.md)  
