# 函数和过程  

## 概览  

`batScript`支持两种可调用的代码块:函数(`func`,有返回值的代码块,通过`return`语句返回结果)和**过程**(`proc`,无返回值的代码块,用于执行操作但不返回结果).

函数和过程都支持参数传递,并且函数支持递归调用. 它们的变量作用域默认限制在内部,但可以通过`glob`关键字访问全局作用域.

## 函数  

函数使用`func`关键字定义,必须有返回值. 基本语法如下:

```batscript
func add(a: num, b: num) -> num
{
    return a + b;
}

# 调用函数
set result = add(5, 3);  # result = 8
```

函数可以调用自身,称为递归:  

```batscript
func factorial(n: num) -> num
{
    if (n <= 1)
    {
        return 1;
    }
    return n * factorial(n - 1);
}

set result = factorial(5);  # result = 120
```

### 作用域规则  

函数内定义的变量默认是局部变量,只在函数内有效:

```batscript
set x = 10;  # global variable

func test() -> num
{
    set x = 20;  # local variable
    return x;
}

set y = test();  # y = 20
# x is still 10
```

如果需要在函数内访问或修改全局变量,使用`glob`关键字(详见下文).

### 缺省参数和参数默认值的实现  

#### 缺省参数  

参数名之前有`--`的参数称为缺省参数,在调用函数时可省略.缺省参数必须在非缺省参数之后.  
该参数默认为该类型的`Nil`:

```batscript
proc sample(--a: num, --b: str)
{
    if (?a)
    {
        echo("a is Nil");
    }
    if (?b)
    {
        echo("b is Nil");
    }
}

sample();  # a == Nil, b == Nil
```

可以通过判空运算符`?`和条件语句来实现参数默认值:

```batscript
func greet(name: str, --greeting: str) -> str
{
    if (?greeting)
    {
        set greeting = "Hello";
    }
    return greeting .. ", " .. name .. "!";
}

set msg1 = greet("Alice", "Hi");   # "Hi, Alice!"
set msg2 = greet("Bob");           # "Hello, Bob!"
```

```batscript
func power(base: num, --exponent: num) -> num
{
    if (&exponent == "nil")
    {
        set exponent = 2;
    }
    
    set result = 1;
    set i = 0;
    while (i < exponent)
    {
        set result = result * base;
        set i = i + 1;
    }
    return result;
}

set sq = power(5);      # 25
set cube = power(5, 3); # 125
```

## 过程  

过程使用`proc`关键字定义,不返回值.  

```batscript
proc printMessage(msg: str)
{
    echo(msg);
}

printMessage("Hello, batScript!");
```

例如,以下代码是一个修改全局状态的过程:  

```batscript
set total = 0;

proc addToTotal(value: num)
{
    glob total;
    set total = total + value;
}

addToTotal(10);
addToTotal(20);
# total is now 30
```

过程除了不返回值外,和函数一致.  

## 使用`glob`访问全局变量

`glob`关键字用于在函数或过程内部访问和操作全局变量.

```batscript
set counter = 0;

func getCounter() -> num
{
    glob counter; # 声明`counter`是全局的`counter`
    return counter;
}
```

可以使用`glob`从全局变量取值到局部变量(非引用,仅取值):

```batscript
set global_value = 100;

func useGlobal() -> num
{
    set local_value: num = glob global_value;
    set local_value = local_value + 10;
    return local_value;  # returns 110
}

set result = useGlobal();  # result = 110
# global_value is still 100
```

也可以直接修改全局变量:

```batscript
set score = 0;

proc addScore(points: num)
{
    glob score;
    set score = score + points;
}

addScore(10);
addScore(5); # score现在是15
```

glob语句必须在函数有效部分的开头.  

## 常用的内置函数和过程  

`batScript`标准库内置了一些函数和过程.以下简述了一些最主要的:  

### Std库

Std库提供基础的输入输出功能,是最常用的库.

#### 输入输出

- `echo(msg: str)`: 输出文本,不换行
- `echoline(msg: str)`: 输出文本并换行
- `pause()`: 暂停程序,等待用户按键

```batscript
lib Std;

echoline("Welcome to batScript!");
echo("Press any key to continue...");
pause();
```

- `input(prompt: str) -> str`: 显示提示并获取用户输入

```batscript
lib Std;

proc greetUser()
{
    set name = input("Enter your name: ");
    echoline("Hello, " .. name .. "!");
}

greetUser();
```

#### 暂停  

- `pause()`: 暂停脚本

```batscript
lib Std;

pause()
```

### Str库

Str库提供字符串处理相关函数.

#### 字符串操作函数  

- `strReplace(source: str, old: str, new: str) -> str`: 替换字符串中的内容
- `strCompare(str1: str, str2: str) -> num`: 比较两个字符串,返回-1(str1<str2)、0(相等)或1(str1>str2)
- `strIndex(source: str, target: str) -> num`: 查找子字符串首次出现的位置,未找到返回-1
- `strCount(source: str, target: str) -> num`: 统计子字符串在源字符串中出现的次数
- `strUpper(s: str) -> str`: 将字符串转为大写
- `strLower(s: str) -> str`: 将字符串转为小写
- `strTrim(s: str) -> str`: 去除字符串首尾空白字符
- `strSplit(source: str, delimiter: str) -> arr<str>`: 按分隔符拆分字符串为数组
- `strJoin(arr: arr<str>, separator: str) -> str`: 用分隔符连接字符串数组
- `strStartsWith(source: str, prefix: str) -> bool`: 判断字符串是否以指定前缀开头
- `strEndsWith(source: str, suffix: str) -> bool`: 判断字符串是否以指定后缀结尾
- `strSubstring(source: str, start: num, length: num) -> str`: 提取子字符串

```batscript
lib Str;
lib Std;

# 替换
set text = "Hello World";
set new_text = strReplace(text, "World", "batScript");  # "Hello batScript"

# 比较
set cmp1 = strCompare("abc", "abd");  # -1
set cmp2 = strCompare("test", "test");  # 0
set cmp3 = strCompare("xyz", "abc");  # 1

# 查找子字符串
set pos = strIndex("batScript is awesome", "Script");  # 3
set not_found = strIndex("Hello", "World");  # -1

# 计次
set count = strCount("banana", "a");  # 3
set count2 = strCount("Hello World", "o");  # 2

# 大小写转换
set upper = strUpper("hello");  # "HELLO"
set lower = strLower("WORLD");  # "world"

# 清楚空白
set trimmed = strTrim("  hello  ");  # "hello"

# 拆分和聚合
set parts = strSplit("a,b,c", ",");  # ["a", "b", "c"]
set joined = strJoin(parts, "-");  # "a-b-c"

# 前缀和后缀检测
set starts = strStartsWith("batScript", "bat");  # True
set ends = strEndsWith("test.bs", ".bs");  # True

# 切片
set sub = strSubstring("Hello World", 0, 5);  # "Hello"

proc processUserInput()
{
    set user_input = input("Enter text: ");
    
    if (strIndex(user_input, "batScript") >= 0)
    {
        echoline("Keyword detected!");
    }
    
    set trimmed = strTrim(user_input);
    set cleaned = strReplace(trimmed, "bad", "***");
    
    set upper = strUpper(cleaned);
    echoline("Processed: " .. upper);
}
```

### Math库

Math库提供数学运算相关的函数.

#### 常用函数  

- `isOdd(n: num) -> bool`: 判断是否为奇数
- `isEven(n: num) -> bool`: 判断是否为偶数
- `floor(n: num) -> num`: 向下取整
- `ceil(n: num) -> num`: 向上取整
- `round(n: num) -> num`: 四舍五入取整
- `pow(base: num, exponent: num) -> num`: 计算幂运算
- `sqrt(n: num) -> num`: 计算平方根
- `abs(n: num) -> num`: 计算绝对值
- `max(a: num, b: num) -> num`: 返回较大值
- `min(a: num, b: num) -> num`: 返回较小值

#### 数学常量  

Math库提供了常用的数学常量:

- `PI`: 圆周率 π (3.14159265358979323846...)
- `PI_SIMP`: 简化的圆周率 (3.14)
- `E`: 自然对数的底 e (2.71828182845904523536...)
- `E_SIMP`: 简化的自然对数底 (2.72)

```batscript
lib Math;
lib Std;

set num = 7;
if (isOdd(num))
{
    echoline("7 is odd");
}

set result = pow(2, 10);     # 1024
set root = sqrt(16);         # 4
set integer = floor(3.14);   # 3
set rounded = round(3.7);    # 4

set circle_area = PI * pow(5, 2);
echoline("Circle area: " .. circle_area);

set approx_area = PI_SIMP * 25;
echoline("Approximate area: " .. approx_area);

set exp_value = pow(E, 2);
echoline("e^2 = " .. exp_value);
```

```batscript
lib Math;

# high precision
func calculateCircleCircumference(radius: num) -> num
{
    return 2 * PI * radius;
}

func estimateCircleCircumference(radius: num) -> num
{
    return 2 * PI_SIMP * radius;
}

set precise = calculateCircleCircumference(10);   # 62.83185307...
set estimated = estimateCircleCircumference(10); # 62.8
```

### Cmd库

Cmd库用于配置命令行窗口的外观和行为.

#### 常用过程  

- `setCmdTitle(title: str)`: 设置窗口标题
- `setCmdColor(foreground: num, background: num)`: 设置文字和背景颜色
- `clearCmd()`: 清空屏幕
- `setCmdSize(width: num, height: num)`: 设置窗口大小

#### 预设颜色常量  

- `CMD_BLACK`: 黑色 (0)
- `CMD_BLUE`: 蓝色 (1)
- `CMD_GREEN`: 绿色 (2)
- `CMD_CYAN`: 青色 (3)
- `CMD_RED`: 红色 (4)
- `CMD_MAGENTA`: 洋红 (5)
- `CMD_YELLOW`: 黄色 (6)
- `CMD_WHITE`: 白色 (7)
- `CMD_GRAY`: 灰色 (8)
- `CMD_LIGHT_BLUE`: 淡蓝 (9)
- `CMD_LIGHT_GREEN`: 淡绿 (10)
- `CMD_LIGHT_CYAN`: 淡青 (11)
- `CMD_LIGHT_RED`: 淡红 (12)
- `CMD_LIGHT_MAGENTA`: 淡洋红 (13)
- `CMD_LIGHT_YELLOW`: 淡黄 (14)
- `CMD_BRIGHT_WHITE`: 亮白 (15)

```batscript
lib Cmd;
lib Std;

setCmdTitle("My batScript Program");
setCmdColor(CMD_LIGHT_GREEN, CMD_BLACK);
clearCmd();
echoline("Using color constants!");

setCmdColor(10, 0);  

setCmdColor(CMD_YELLOW, CMD_BLUE);
echoline("Warning message");

setCmdColor(CMD_RED, CMD_WHITE);
echoline("Error message");

setCmdColor(CMD_WHITE, CMD_BLACK); 
```

```batscript
lib Cmd;
lib Std;

proc setupWindow()
{
    setCmdTitle("Data Analysis Tool v1.0");
    setCmdSize(100, 30);
    setCmdColor(CMD_LIGHT_CYAN, CMD_BLACK);
    clearCmd();
}

setupWindow();
echoline("Window configured!");
```

> **完整库参考**: 更多库和详细的函数/过程说明,请参考[库索引](../../manual/库/库索引.md)

---  
**导航**  

[上一个](./06-控制流.md) | [下一个](./08-模块管理.md)  
[教程目录](./01-教程目录.md) | [手册导览](../manual/手册导引.md) | [返回 README](../../../README-zh.md)